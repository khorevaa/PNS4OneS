#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура pns4ones_ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Или Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("НоваяЗадача", ЭтоНовый());
	
КонецПроцедуры

&После("ПриЗаписи")
Процедура pns4ones_ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Или Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.НоваяЗадача
			И ЗначениеЗаполнено(Исполнитель)
			И ТипЗнч(Исполнитель) = Тип("СправочникСсылка.Пользователи")
	Тогда
		ОтправитьУведомлениеИсполнителю();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьУведомлениеИсполнителю()
	
	Оповещение = pns4ones_СервисУведомленийКлиентСервер.ИнициализироватьОповещение();
	Оповещение.Текст = НСтр("ru='Создана новая задача'");
	Оповещение.Пояснение = Наименование;
	Оповещение.Статус = СтатусОповещенияПользователя.Важное;
	Оповещение.ДействиеПриНажатии = ПолучитьНавигационнуюСсылку(Ссылка);
	
	Сообщение = pns4ones_СервисУведомленийКлиентСервер.ИнициализироватьСообщение();
	Сообщение.Оповещение = Оповещение;
	
	ИмяИсполнителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Исполнитель, "Наименование");
	ИдентификаторПользователя = pns4ones_СервисУведомлений.ПолучитьИдентификаторПользователя(ИмяИсполнителя);
	Получатель = pns4ones_СервисУведомленийКлиентСервер.ПолучательПользователь(ИдентификаторПользователя);
	
	Результат = pns4ones_СервисУведомлений.ОтправитьУведомление(Получатель, Сообщение);
	
	Если Не Результат.Успешно Тогда
		pns4ones_СервисУведомленийВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(Результат.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли