///////////////////////////////////////////////////////////////////////////////////////////////////////
// (с) Tolkachev Pavel, 2021-2022
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КаналОповещения = Строка(Новый УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикПрогрессаДлительнойОперации", ЭтотОбъект);
	pns4ones_СервисУведомленийКлиент.ОтключитьОбработчикУведомлений(Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьДлительнуюОперацию(Команда)
	
	ДлительнаяОперация = НачатьДлительнуюОперациюНаСервере();                
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ЗавершениеДлительнойОперации", ЭтотОбъект);
	
	Состояние(НСтр("ru='Выполняется обработка...'"));
	
	ОбработчикПрогресса = Новый ОписаниеОповещения("ОбработчикПрогрессаДлительнойОперации", ЭтотОбъект);
	pns4ones_СервисУведомленийКлиент.ПодключитьОбработчикУведомлений(ОбработчикПрогресса, КаналОповещения); 
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработчикПрогрессаДлительнойОперации(Сообщение, ДопПараметры) Экспорт
	
	Сообщение.СтандартнаяОбработка = Ложь;
	
	Прогресс = Сообщение.Данные.Прогресс;
	ВыполняемоеДействие = Сообщение.Данные.ВыполняемоеДействие;
	
КонецПроцедуры

&НаСервере
Функция НачатьДлительнуюОперациюНаСервере()
	
	ПараметрыДлительнойОперации = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Получатель", pns4ones_СервисУведомлений.ПолучитьИдентификаторПользователя());
	ПараметрыВыполнения.Вставить("КаналОповещения", КаналОповещения);
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ЭтоВнешняяОбработка", Ложь);
	ПараметрыМетода.Вставить("ИмяОбработки", Метаданные.Обработки.pns4ones_ДемоДлительнаяОперацияНаСервереСОтображениемПрогресса.Имя);
	ПараметрыМетода.Вставить("ИмяМетода", "ВыполнитьДлительнуюОперацию");
	ПараметрыМетода.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыДлительнойОперации,
		"ДлительныеОперации.ВыполнитьПроцедуруМодуляОбъектаОбработки",
		ПараметрыМетода,
		""
	);
	
КонецФункции

&НаКлиенте
Процедура ЗавершениеДлительнойОперации(Результат, ДопПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикПрогрессаДлительнойОперации", ЭтотОбъект);
	pns4ones_СервисУведомленийКлиент.ОтключитьОбработчикУведомлений(Оповещение);
	
	Прогресс = 0;
	ВыполняемоеДействие = "";
	
	ПоказатьОповещениеПользователя(НСтр("ru='Операция завершена'"));
	
КонецПроцедуры

#КонецОбласти
