#Область ПрограммныйИнтерфейс

Процедура ВыполнитьДлительнуюОперацию(Параметры, АдресХранилища) Экспорт
	
	ВыполняемыеДействия = Новый Соответствие;
	ВыполняемыеДействия.Вставить(0, НСтр("ru='Загрузка данных...'"));
	ВыполняемыеДействия.Вставить(20, НСтр("ru='Анализ загруженных данных...'"));
	ВыполняемыеДействия.Вставить(70, НСтр("ru='Сохранение обработанных результатов...'"));
	
	ТекущееДействие = Неопределено;
	Пауза = 100;
	
	Для Прогресс = 0 По 100 Цикл
		
		ВыполняемоеДействие = ВыполняемыеДействия[Прогресс];
		Если ВыполняемоеДействие <> Неопределено Тогда
			ТекущееДействие = ВыполняемоеДействие;
		КонецЕсли;
		
		ОповеститьКлиентаОПрогрессе(Параметры.Получатель, ТекущееДействие, Прогресс, Параметры.КаналОповещения);
		
		ВремяОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах() + Пауза;
		Пока ТекущаяУниверсальнаяДатаВМиллисекундах() < ВремяОкончания Цикл
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОповеститьКлиентаОПрогрессе(ИдентификаторПолучателя, ВыполняемоеДействие, Прогресс, КаналОповещения)
	
	Данные = Новый Структура;
	Данные.Вставить("Прогресс", Формат(Прогресс, "ЧГ="));
	Данные.Вставить("ВыполняемоеДействие", ВыполняемоеДействие);
	
	Сообщение = pns4ones_СервисУведомленийКлиентСервер.ИнициализироватьСообщение();
	Сообщение.Тема = КаналОповещения;
	Сообщение.Данные = Данные;
	
	Получатель = pns4ones_СервисУведомленийКлиентСервер.ПолучательПользователь(ИдентификаторПолучателя);
	
	pns4ones_СервисУведомлений.ОтправитьУведомление(Получатель, Сообщение);
	
КонецПроцедуры

#КонецОбласти