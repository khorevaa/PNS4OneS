///////////////////////////////////////////////////////////////////////////////////////////////////////
// (с) Tolkachev Pavel, 2021-2022
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОпределитьИспользованиеСервисаУведомлений(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбновитьСостояниеКомпоненты", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "pns4ones_ИзмененыНастройкиСервисаУведомлений"
			Или ИмяСобытия = "pns4ones_ПодключениеКСервисуУстановлено"
			Или ИмяСобытия = "pns4ones_СервисУведомленийОтключен"
			Или ИмяСобытия = "pns4ones_ОшибкаПриПодключенииКСервису"
	Тогда
		ОпределитьИспользованиеСервисаУведомлений(ЭтаФорма);
		ОбновитьСостояниеКомпоненты();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НадписьСостояниеКомпонентыОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка
)

	Если СтрНачинаетсяС(НавигационнаяСсылкаФорматированнойСтроки, "e1cib/") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "УстановитьКомпоненту" Тогда
		
		Состояние(, , НСтр("ru='Выполнятся установка компоненты сервиса уведомлений...'"));
		pns4ones_СервисУведомленийКлиент.НачатьПодключениеКомпоненты(Истина);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "УстановитьСоединение" Тогда
		
		Состояние(, , НСтр("ru='Выполнятся соединение с сервисом уведомлений...'"));
		pns4ones_СервисУведомленийКлиент.НачатьПодключениеКомпоненты(Ложь);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьСостояниеКомпоненты()
	
	Состояние(, , НСтр("ru='Получение информации о состоянии компоненты сервиса уведомлений...'"));
	
	Если Не СервисУведомленийИспользуется Тогда
		
		УстановитьСостояниеСервисНеИспользуется();
		
	ИначеЕсли глПараметрыСервисаУведомлений.Компонента = Неопределено Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПроверкаПодключенияКомпонентыЗавершение", ЭтотОбъект);
		pns4ones_СервисУведомленийКлиент.НачатьПроверкуПодключенияКомпоненты(Оповещение);
		
	ИначеЕсли Не глПараметрыСервисаУведомлений.ПодключениеУстановлено Тогда
		
		УстановитьСостояниеПодключениеНеУстановлено();
		
	Иначе
		
		УстановитьСостояниеПодключениеУстановлено();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаПодключенияКомпонентыЗавершение(Подключено, ДопПараметры) Экспорт
	
	Если Подключено Тогда
		УстановитьСостояниеПодключениеНеУстановлено();
	Иначе
		УстановитьСостояниеКомпонентаНеУстановлена();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеСервисНеИспользуется()
	
	СсылкаПерейтиКНастройкам = ГиперссылкаДляСостоянияКомпоненты(
		НСтр("ru='Перейти к настройкам...'"),
		"e1cib/command/Обработка.pns4ones_НастройкаСервисаУведомлений.Команда.НастройкаСервисаУведомлений"
	);
	УстановитьСостояниеКомпоненты(
		НСтр("ru='Сервис уведомлений PNS4OneS не используется.'"),
		СсылкаПерейтиКНастройкам
	);
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеКомпонентаНеУстановлена()
	
	СсылкаУстановитьКомпоненту = ГиперссылкаДляСостоянияКомпоненты(
		НСтр("ru='Установить компоненту'"),
		"УстановитьКомпоненту"
	);
	УстановитьСостояниеКомпоненты(
		НСтр("ru='Компонента подключения к сервису уведомлений PNS4OneS не установлена.'"),
		СсылкаУстановитьКомпоненту
	);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеПодключениеНеУстановлено()
	
	УстановитьСоединение = ГиперссылкаДляСостоянияКомпоненты(
		НСтр("ru='Подключиться к сервису'"),
		"УстановитьСоединение"
	);
	УстановитьСостояниеКомпоненты(
		НСтр("ru='Соединение с сервисом уведомлений PNS4OneS не установлено.'"),
		УстановитьСоединение
	);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеПодключениеУстановлено()
	
	УстановитьСостояниеКомпоненты(НСтр("ru='Установлено соединение с сервисом уведомлений PNS4OneS.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеКомпоненты(ТекстСостояния, Гиперссылка = Неопределено)
	
	Состояние(Неопределено);
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(ТекстСостояния);
	
	Если Гиперссылка <> Неопределено Тогда
		МассивСтрок.Добавить("    ");
		МассивСтрок.Добавить(Гиперссылка);
	КонецЕсли;
	
	Элементы.НадписьСостояниеКомпоненты.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Функция ГиперссылкаДляСостоянияКомпоненты(Текст, Ссылка)
	
	Возврат Новый ФорматированнаяСтрока(Текст, , , , Ссылка);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОпределитьИспользованиеСервисаУведомлений(Форма)
	
	Форма.СервисУведомленийИспользуется = СервисУведомленийИспользуется();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СервисУведомленийИспользуется()
	
	НастройкиСервиса = pns4ones_СервисУведомлений.НастройкиСервисаУведомлений();
	Возврат НастройкиСервиса.Используется;
	
КонецФункции

#КонецОбласти
